#! /usr/bin/env perl

use strict;
use warnings;
use Try::Tiny;
use Socket;
use POSIX ":sys_wait_h";
use lib 't/lib';
use lib 'lib';
use Selenium::TestUtil;
my $util= 'Selenium::TestUtil';

sub die_usage {
	my $msg= shift;
	die "$msg\n".<<'END';
Usage: [env_vars] script/prove-with-testserver [prove_args]
  where prove_args are passed directly to 'prove', and env_vars are:
  SELENIUM_HOST=$HOSTNAME[:$PORT]      (required)
  SELENIUM_PORT=$PORT                  (optional, default 4444)
  SELENIUM_BROWSER=$BROWSER_NAME       (argument to Selenium Driver)
  TEST_APP_HOST=$ADDRESS_OF_THIS_HOST  (if autodetection fails)
  TEST_APP_PORT=$LOCAL_PORT            (to used a specific port locally)
  TEST_APP_STARTUP_TIMEOUT=$SECONDS    (default 20)

END
}

die_usage "No SELENIUM_HOST specified"
	unless defined $util->selenium_host;

$util->test_app_host(
	$util->find_address_facing_selenium
	or die "Can't guess value for TEST_APP_HOST. Please set manually "
		."to the IP addr SELENIUM_HOST should use to connect back to us.\n"
);
$util->test_app_port(
	$util->find_port_facing_selenium
	or die "Can't find a free port to run the app on.\n"
);

$ENV{TEST_APP_STARTUP_TIMEOUT} ||= 20;

# Start an instance of the webapp
warn "Starting webapp on ".$util->test_app_host." port ".$util->test_app_port."\n";

defined (my $server_pid= fork()) or die "fork(): $!";

exec_server_or_exit() if 0 == $server_pid;

my $result= 2;
try {
	if (!wait_for_server_ready()) {
		warn "Not running 'prove'\n";
	}
	else {
		$result= system('prove', @ARGV);
		if ($result < 0) {
			warn "Can't exec 'prove': $!\n";
			$result= 2;
		}
	}
} catch {
	warn "exception: $_\n";
};
if (defined $server_pid) {
	warn "Terminating webapp\n";
	kill TERM => $server_pid;
	waitpid($server_pid, 0);
}
exit($result);

sub exec_server_or_exit {
	use FindBin;
	my $script= "$FindBin::Bin/../script/ra_infosite_server.pl";
	close STDIN;
	exec $script, '-p', $util->test_app_port
		or warn "exec($script): $!\n";
	# exec failed, but we still want exec() semantics, if possible.
	# i.e. no destructors of objects that might use handles shared with the parent.
	exec 'false' or warn "exec(false): $!\n";
	exit(1);
}

sub wait_for_server_ready {
	my $quit= 0;
	$SIG{TERM}= sub { $quit= 1; };
	
	socket(my $socket, Socket::PF_INET, Socket::SOCK_STREAM, 0)
		or die "socket(): $!";
	my $deadline= time + $ENV{TEST_APP_STARTUP_TIMEOUT};
	my $result= '';
	while (!$quit) {
		if (connect($socket, pack_sockaddr_in($util->test_app_port, inet_aton('localhost')))) {
			$result= 1;
			last;
		}
		if (waitpid($server_pid, WNOHANG) == $server_pid) {
			warn "Server terminated during startup\n";
			$server_pid= undef;
			last;
		}
		if (time > $deadline) {
			warn "Server not ready after $ENV{TEST_APP_STARTUP_TIMEOUT} seconds. "
				."You might need to adjust TEST_APP_STARTUP_TIMEOUT\n";
			last;
		}
		select(undef, undef, undef, 0.2); # sleep 0.2;
	}
	close $socket;
	return $result;
}
